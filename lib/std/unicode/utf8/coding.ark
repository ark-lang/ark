TRAILING_BYTES := []u8{
	0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
    1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1, 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,
    2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2, 3,3,3,3,3,3,3,3,4,4,4,4,5,5,5,5
};

OFFSETS := []u32{
	0x00000000, 0x00003080, 0x000E2080,
    0x03C82080, 0xFA082080, 0x82082080
};

RUNE_MAX_BYTES := 4;

pub func numRunes(src: []u8) -> uint {
	mut i: uint = 0;
	mut num: uint = 0;

	for i < len(src) {
		nb := uint(TRAILING_BYTES[src[i]]);
		i += nb + 1;
		num += 1;
	}

	return num;
}

pub func numBytes(src: []rune) -> uint {
	mut i: uint = 0;
	mut num: uint = 0;

	for i < len(src) {
		ch := src[i];
		if ch < 0x80 {
			num += 1;
		} else if ch < 0x800 {
			num += 2;
        } else if ch < 0x10000 {
        	num += 3;
        } else if ch < 0x110000 {
        	num += 4;
        }
        i += 1;
	}
	return num;
}

pub func decode(mut dest: []rune, src: []u8) -> uint {
	mut srcIndex: uint = 0;
	mut destIndex: uint = 0;

	for destIndex < len(dest) {
		nb := uint(TRAILING_BYTES[src[srcIndex]]);
		if srcIndex + nb >= len(src) {
			// Src to short, so we can't read more
			return destIndex;
		}

		mut ch: rune = 0;
		if nb >= 3 {
			ch += rune(src[srcIndex]); ch <<= 6;
			srcIndex += 1;
		}
		if nb >= 2 {
			ch += rune(src[srcIndex]); ch <<= 6;
			srcIndex += 1;
		}
		if nb >= 1 {
			ch += rune(src[srcIndex]); ch <<= 6;
			srcIndex += 1;
		}
		ch += rune(src[srcIndex]);
		srcIndex += 1;

		ch -= rune(OFFSETS[nb]);
		dest[destIndex] = ch;
		destIndex += 1;
	}

	return destIndex;
}

pub func encode(mut dest: []u8, src: []rune) -> uint {
	mut srcIndex: uint = 0;
	mut destIndex: uint = 0;
	for srcIndex < len(src) {
		ch := src[srcIndex];
		if ch < 0x80 {
			if destIndex >= len(dest) {
				return destIndex;
			}
			dest[destIndex] = u8(ch);
			destIndex += 1;
		}
		else if ch < 0x800 {
			if destIndex >= len(dest)-1 {
				return destIndex;
			}
			dest[destIndex + 0] = u8((ch>>6) | 0xC0);
            dest[destIndex + 1] = u8((ch & 0x3F) | 0x80);
            destIndex += 2;
        }
        else if ch < 0x10000 {
			if destIndex >= len(dest)-2 {
				return destIndex;
			}
            dest[destIndex + 0] = u8((ch>>12) | 0xE0);
            dest[destIndex + 1] = u8(((ch>>6) & 0x3F) | 0x80);
            dest[destIndex + 2] = u8((ch & 0x3F) | 0x80);
            destIndex += 3;
        }
        else if ch < 0x110000 {
			if destIndex >= len(dest)-3 {
				return destIndex;
			}
            dest[destIndex + 0] = u8((ch>>18) | 0xF0);
            dest[destIndex + 1] = u8(((ch>>12) & 0x3F) | 0x80);
            dest[destIndex + 2] = u8(((ch>>6) & 0x3F) | 0x80);
            dest[destIndex + 3] = u8((ch & 0x3F) | 0x80);
            destIndex += 4;
        }
        srcIndex += 1;
	}

	return destIndex;
}
